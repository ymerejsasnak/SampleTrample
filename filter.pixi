
//adapted from  Alexander Zolotov's code
//
// Based on Cookbook formulae for audio EQ biquad filter coefficients
// by Robert Bristow-Johnson  <rbj@audioimagination.com>
//


fn set_filter_parameters( $this, $type, $q, $freq )
{
    //temp
    sample_rate = 44100
    
    
    $omega = 2 * M_PI * $freq / sample_rate
    $tsin = sin( $omega )
    $tcos = cos( $omega )
    $alpha = $tsin / ( 2 * $q )
    
    if $type == 0
    {
	//Low-pass:
	$b0 = ( 1 - $tcos ) / 2
	$b1 = 1 - $tcos
	$b2 = ( 1 - $tcos ) / 2
	$a0 = 1 + $alpha
	$a1 = -2 * $tcos
	$a2 = 1 - $alpha
    }
    if $type == 1
    {
	//High-pass
	$b0 = ( 1 + $tcos ) / 2
	$b1 = -( 1 + $tcos )
	$b2 = ( 1 + $tcos ) / 2
	$a0 = 1 + $alpha
	$a1 = -2 * $tcos
	$a2 = 1 - $alpha
    }
    if $type == 2
    {
	//Band-pass:
	$b0 = $alpha
    $b1 = 0
	$b2 = -$alpha
	$a0 = 1 + $alpha
	$a1 = -2 * $tcos
	$a2 = 1 - $alpha
    }
    
    
    $filter_a = new( 3, 1, FLOAT32 ) //feedforward filter coefficients
    $filter_b = new( 2, 1, FLOAT32 ) //feedback filter coefficients

    $filter_a[ 0 ] = $b0 / $a0
    $filter_a[ 1 ] = $b1 / $a0
    $filter_a[ 2 ] = $b2 / $a0
    $filter_b[ 0 ] = -$a1 / $a0
    $filter_b[ 1 ] = -$a2 / $a0
    init_filter( $this, $filter_a, $filter_b )
}
