fn create_wave_window($data, $x, $y, $xsize, $ysize)
{
    $this = new()
    
    $this.data = $data
    $this.x = $x
    $this.y = $y
    $this.xsize = $xsize
    $this.ysize = $ysize
    
    $this.sample_info = load_sample($this.data)
    
    $this.plotmax = new($xsize)
    $this.plotmin = new($xsize)
                
    
    
    $i = 0
    $step = get_size($this.data) / $xsize
    while ($i < $xsize)
    {
        $max = $this.data[$i * $step]
        $min = $this.data[$i * $step]
        
        $j = 1
        while ($j < $step)
        {
            if $this.data[$i * $step + $j] > $max
                {$max = $this.data[$i * $step + $j]}
            
            if $this.data[$i * $step + $j] < $min
                {$min = $this.data[$i * $step + $j]}
                
            $j + 1
        }
        
        $this.plotmax[$i] = scale($max, -(1<<15), (1<<15), -$this.ysize/2, $this.ysize/2)       
        $this.plotmin[$i] = scale($min, -(1<<15), (1<<15), -$this.ysize/2, $this.ysize/2)
        
        $i + 1
    }
    
    
 
    $this.update =
    {
        $this = $1
        $touch_state = $2
        
        if $touch_state.down && 
             $touch_state.x > $this.x && $touch_state.x < $this.x + $this.xsize &&
             $touch_state.y > $this.y && $touch_state.y < $this.y + $this.ysize
        {
            
            $pos = scale($touch_state.x, $this.x, $this.x + $this.xsize, 0, get_size($this.data))
            $this.sample_info[SMP_SRC_OFF_H] = floor($pos)
            $this.sample_info[SMP_SRC_OFF_L] = $pos - floor($pos)
            play_wav($this.sample_info)
        }
        
        
    }
        
    
    
    
    fn _draw_wave($this)
    {
    
        fbox($this.x, $this.y, $this.xsize, $this.ysize, get_blend(BLACK, WHITE, 40))
        $midline = $this.y + $this.ysize / 2
        
        $i = 0
        while ($i < $this.xsize)
        {
            line($i + $this.x, $midline + $this.plotmax[$i], $i + $this.x, $midline + $this.plotmin[$i], GREEN)
            $i + 1
        }
        
        //box($this.x, $this.y, $this.xsize, $this.ysize, WHITE)
        line($this.x, $midline, $this.x + $this.xsize, $midline, WHITE)
        
        $pos = scale($this.sample_info[ SMP_SRC_OFF_H ], 0, get_size($this.data), $this.x, $this.x + $this.xsize)       
        line($pos, $this.y, $pos, $this.y + $this.ysize, RED)
        
    }   
    
    
    
    
    $this.draw = _draw_wave
    
    
    ret ($this)
}
